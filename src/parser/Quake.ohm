Quake {
    // needed because space from built-in ohm rules consider \n as space
    space := " " | "\t"

    AllGames = Game (~end Game)*
	
    Game = SeparatorToken? InitGameToken ActionTokens* ShutdownGameToken? SeparatorToken
    
    SeparatorToken = LinePrefix separator eol?
    
    InitGameToken = CommandToken<"InitGame">
    ShutdownGameToken = CommandToken<"ShutdownGame">
	
    ActionTokens = ClientUserInfoChangedToken
    			  | KillToken
                  | IgnoredCommandToken
	
    ClientUserInfoChangedToken = LinePrefix "ClientUserinfoChanged:" identifier "n\\" userName "\\t" rest
    KillToken = LinePrefix "Kill:" identifier identifier identifier ":" rest
    
    CommandToken<command> = LinePrefix command ":" rest
    IgnoredCommandToken = LinePrefix (~commands letter)+ ":" rest

    // utils
    LinePrefix = digit+ ":" digit+
    separator = "------------------------------------------------------------"
    
    identifier = digit+
    userName = (~"\\t" any)+

    commands = separator
             | "InitGame"
             | "ShutdownGame"
             | "ClientUserinfoChanged"

    rest = (~eol any)* eol
    eol = "\n"
}